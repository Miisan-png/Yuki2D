var Y = require("stdlib.ys", "Y");
var M = require("yuki_game.ys");
var Game = M.Game;

var sheet_id = -1;

fn ensure_sheet() {
    if (sheet_id >= 0) return;
    sheet_id = load_sprite_sheet("asset_pack/characters/player.png", 48, 48);
}

fn make_anim(row, fps) {
    ensure_sheet();
    var a = Y.Anim.new(sheet_id, Game.row_frames(row, 6), fps, true);
    a.set_origin(24, 24);
    a.set_scale(2, 2);
    return a;
}

fn spawn(ctx, world, particles, x, y) {
    var idle_down = make_anim(0, 4);
    var idle_right = make_anim(1, 4);
    var idle_up = make_anim(2, 4);
    var move_down = make_anim(3, 10);
    var move_right = make_anim(4, 10);
    var move_up = make_anim(5, 10);

    var col_w = 32;
    var col_h = 32;
    var col_offset_y = 4;
    var cid = collider_create(x - col_w * 0.5, y - col_h * 0.5 + col_offset_y, col_w, col_h, "player", true);
    var collider = { id: cid, w: col_w, h: col_h, ox: 0, oy: col_offset_y };

    var e = world.spawn({
        x: x,
        y: y,
        tag: "player",
        collider: collider
    });

    e.hp = 100;
    e.speed = 140;
    e.facing = "down";
    e.anim = idle_down;
    e.anim_offset_x = -48;
    e.anim_offset_y = -48;
    e._idle_down = idle_down;
    e._idle_right = idle_right;
    e._idle_up = idle_up;
    e._move_down = move_down;
    e._move_right = move_right;
    e._move_up = move_up;
    e._all = [idle_down, idle_right, idle_up, move_down, move_right, move_up];
    e._dust = particles;
    e._dust_accum = 0;
    e.debug = false;

    e.on_update = fn(dt, ctx2, world2, self) {
        var mx = 0;
        var my = 0;
        if (ctx2.input != nil and ctx2.input.down("a")) mx = mx - 1;
        if (ctx2.input != nil and ctx2.input.down("d")) mx = mx + 1;
        if (ctx2.input != nil and ctx2.input.down("w")) my = my - 1;
        if (ctx2.input != nil and ctx2.input.down("s")) my = my + 1;

        var moving = mx != 0 or my != 0;
        if (moving and mx != 0 and my != 0) {
            mx = mx * 0.7071;
            my = my * 0.7071;
        }

        self.vx = mx * self.speed;
        self.vy = my * self.speed;

        if (moving) {
            if (abs(mx) > abs(my)) {
                if (mx > 0) self.facing = "right";
                else self.facing = "left";
            } else {
                if (my > 0) self.facing = "down";
                else self.facing = "up";
            }
        }

        var flip = self.facing == "left";
        self._idle_right.set_flip(flip, false);
        self._move_right.set_flip(flip, false);

        var active = self._idle_down;
        if (moving) {
            if (self.facing == "down") active = self._move_down;
            else if (self.facing == "up") active = self._move_up;
            else active = self._move_right;
        } else {
            if (self.facing == "down") active = self._idle_down;
            else if (self.facing == "up") active = self._idle_up;
            else active = self._idle_right;
        }
        self.anim = active;

        var i = 0;
        while (i < len(self._all)) {
            var a = self._all[i];
            if (a.id != self.anim.id) a.stop();
            i = i + 1;
        }

        if (ctx2.input != nil and ctx2.input.pressed("tab")) self.debug = !self.debug;

        if (moving and self._dust != nil) {
            self._dust_accum = self._dust_accum + dt;
            if (self._dust_accum >= 0.035) {
                self._dust_accum = 0;
                self._dust.emit(self.x, self.y + 18, 2, {
                    speed: 30,
                    jitter: 16,
                    life: 0.3,
                    size: 2,
                    r: 0.8,
                    g: 0.85,
                    b: 0.9,
                    gravity: 10,
                    drag: 6
                });
            }
        }
    };

    e.on_draw = fn(dt, ctx2, world2, self) {
        self.anim.play(false);
        self.anim.draw();
        if (self.debug and self.collider != nil and self.collider.id != nil) {
            var c = self.collider;
            var px = self.render_x;
            var py = self.render_y;
            if (px == nil) px = self.x;
            if (py == nil) py = self.y;
            var left = px - c.w * 0.5 + c.ox;
            var top = py - c.h * 0.5 + c.oy;
            draw_rect(left - 2, top - 2, c.w + 4, 2, 0.2, 1, 0.2, 1);
            draw_rect(left - 2, top + c.h, c.w + 4, 2, 0.2, 1, 0.2, 1);
            draw_rect(left - 2, top, 2, c.h, 0.2, 1, 0.2, 1);
            draw_rect(left + c.w, top, 2, c.h, 0.2, 1, 0.2, 1);
        }
    };

    return e;
}

exports = { spawn: spawn };
