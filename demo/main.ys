var Y = require("stdlib.ys", "Y");
var Scene = require("scene.ys", "Scene");
var UI = require("ui.ys", "UI");

var ctx;
var show_ui = true;
var ui_name = "Yuki2D";
var ui_zoom = 1.0;
var ui_scale = 1.25;
var ui_font_path = "../assets/fonts/ui.ttf";
var ui_font_size = 18;
var ui_font_id = -1;
var ui_accent = [0.55, 0.78, 0.95, 1.0];
var ui_combo_items = ["Move", "Edit", "Debug"];
var ui_combo_idx = 0;
var ui_drag_f = 0.0;
var ui_drag_i = 0;
var sheet_id = -1;
var ui_progress = 0.0;
var show_modal = false;
var selected_scene = "menu";
var show_inspector = true;

fn init() {
    set_virtual_resolution(640, 360);
    if (UI != nil) {
        UI.theme("yuki");
        UI.set_scale(ui_scale);
    }
    ctx = {
        input: Y.Input,
        camera: Y.Camera,
        font: load_font("../assets/fonts/monogram_bitmap.png", "../assets/fonts/monogram_bitmap.json"),
        scene: nil,
        debug: nil,
        pixel_snap: true
    };
    ctx.scene = Scene.create(ctx);
    ctx.scene.set("scenes/menu.ys");

    sheet_id = load_sprite_sheet("asset_pack/characters/player.png", 48, 48);
}

fn update(dt) {
    ctx.scene.tick(dt);
    if (ctx.input != nil and ctx.input.pressed("i")) show_ui = !show_ui;
    ui_progress = ui_progress + dt * 0.25;
    if (ui_progress > 1.0) ui_progress = 0.0;

    if (UI != nil and UI.enabled() and show_ui) {
        UI.set_next_window_pos(12, 12, UI.C.Cond.Once);
        var st = UI.window("Player Inspector", fn() {
            UI.text("Toggle UI: I");
            UI.text("Tab: player collider debug");
            UI.separator();

            if (UI.button("Menu")) ctx.scene.set("scenes/menu.ys");
            UI.same_line();
            if (UI.button("Game")) ctx.scene.set("scenes/game.ys");
            UI.separator();

            if (ctx.debug == nil or ctx.debug.player == nil) {
                UI.text("No player loaded (start Game).");
                return;
            }

            var p = ctx.debug.player;
            UI.text("pos: " + p.x + ", " + p.y);
            if (p.render_x != nil and p.render_y != nil) {
                UI.text("render: " + p.render_x + ", " + p.render_y);
            }
            UI.text("vel: " + p.vx + ", " + p.vy);
            UI.text("facing: " + p.facing);

            if (p.hp != nil) {
                p.hp = UI.drag_int("HP", p.hp, 1, 0, 999);
            }
            if (p.speed != nil) {
                p.speed = UI.slider_float("Speed", p.speed, 40, 360);
            }
            if (p.debug != nil) {
                p.debug = UI.checkbox("Draw collider", p.debug);
            }

            UI.separator();
            ui_zoom = UI.slider_float("Camera Zoom", ui_zoom, 0.5, 3.0);
            if (ctx.camera != nil) ctx.camera.zoom(ui_zoom);
            ctx.pixel_snap = UI.checkbox("Pixel snap render", ctx.pixel_snap == true);
            if (ctx.debug != nil and ctx.debug.camera != nil) {
                var cm = ctx.debug.camera;
                cm.room_mode = UI.checkbox("Room camera", cm.room_mode == true);
                cm.smooth = UI.checkbox("Smooth follow", cm.smooth == true);
                cm.pixel_snap = UI.checkbox("Camera pixel snap", cm.pixel_snap == true);
            }

            UI.separator();
            UI.progress(ui_progress, "demo");
        }, show_inspector, UI.C.WindowFlags.AlwaysAutoResize);
        show_inspector = st.open;
    }
}
