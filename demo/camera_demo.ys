var Y = require("stdlib.ys", "Y");

var x = 0;
var y = 0;
var zoom = 1.0;
var facing = "down";
var room_mode = true;
var room_w = 640;
var room_h = 360;
var pixel_snap = true;
var idle_down;
var idle_up;
var idle_right;
var move_down;
var move_up;
var move_right;
var anims;
var active;

fn init() {
    set_virtual_resolution(640, 360);
    camera_follow_enable(true);
    camera_follow_lerp(8);
    camera_set_zoom(1.0);
    camera_set_deadzone(0, 0);
    camera_set_pixel_snap(pixel_snap);
    camera_set_bounds(-2000, -2000, 4000, 4000);
    set_clear_color(0.05, 0.06, 0.08);

    var sheet = load_sprite_sheet("asset_pack/characters/player.png", 48, 48);

    fn row_frames(row) {
        var frames = [];
        var cols = 6;
        var start = row * cols;
        var i = 0;
        while (i < cols) {
            push(frames, start + i);
            i = i + 1;
        }
        return frames;
    }

    idle_down = Y.Anim.new(sheet, row_frames(0), 4, true);
    idle_right = Y.Anim.new(sheet, row_frames(1), 4, true);
    idle_up = Y.Anim.new(sheet, row_frames(2), 4, true);
    move_down = Y.Anim.new(sheet, row_frames(3), 10, true);
    move_right = Y.Anim.new(sheet, row_frames(4), 10, true);
    move_up = Y.Anim.new(sheet, row_frames(5), 10, true);

    anims = [idle_down, idle_right, idle_up, move_down, move_right, move_up];
    var i2 = 0;
    while (i2 < len(anims)) {
        anims[i2].set_origin(24, 24);
        anims[i2].set_scale(2, 2);
        anims[i2].set_pos(x, y);
        i2 = i2 + 1;
    }
    active = idle_down;
}

fn draw_grid(step, half) {
    var x = -half;
    while (x <= half) {
        var c = 0.12;
        if (x == 0) c = 0.3;
        draw_rect(x, -half, 2, half * 2, c, c, c);
        x = x + step;
    }
    var y = -half;
    while (y <= half) {
        var c2 = 0.12;
        if (y == 0) c2 = 0.3;
        draw_rect(-half, y, half * 2, 2, c2, c2, c2);
        y = y + step;
    }
    draw_rect(-6, -6, 12, 12, 0.9, 0.2, 0.2);
    draw_rect(400, 200, 40, 40, 0.2, 0.5, 0.9);
    draw_rect(-300, 120, 30, 60, 0.8, 0.7, 0.2);
    draw_rect(120, -240, 80, 20, 0.2, 0.8, 0.4);
}

fn update(dt) {
    var speed = 140;
    var mx = 0;
    var my = 0;
    if (Y.Input.down("a") or Y.Input.down("left")) mx = mx - 1;
    if (Y.Input.down("d") or Y.Input.down("right")) mx = mx + 1;
    if (Y.Input.down("w") or Y.Input.down("up")) my = my - 1;
    if (Y.Input.down("s") or Y.Input.down("down")) my = my + 1;
    var moving = mx != 0 or my != 0;
    if (moving) {
        if (mx != 0 and my != 0) {
            mx = mx * 0.7071;
            my = my * 0.7071;
        }
        x = x + mx * speed * dt;
        y = y + my * speed * dt;
        if (abs(mx) > abs(my)) {
            if (mx > 0) facing = "right";
            else facing = "left";
        } else {
            if (my > 0) facing = "down";
            else facing = "up";
        }
    }

    var flip = facing == "left";
    idle_right.set_flip(flip, false);
    move_right.set_flip(flip, false);

    if (moving) {
        if (facing == "down") active = move_down;
        else if (facing == "up") active = move_up;
        else active = move_right;
    } else {
        if (facing == "down") active = idle_down;
        else if (facing == "up") active = idle_up;
        else active = idle_right;
    }

    var i3 = 0;
    while (i3 < len(anims)) {
        anims[i3].set_pos(x, y);
        if (anims[i3].id != active.id) anims[i3].stop();
        i3 = i3 + 1;
    }

    var sc = active.get_scale();
    var px = x + 24 * sc.x;
    var py = y + 24 * sc.y;

    if (Y.Input.pressed("1")) {
        room_mode = !room_mode;
        if (room_mode) camera_set_deadzone(0, 0);
        else camera_set_deadzone(120, 80);
    }
    if (Y.Input.pressed("2")) {
        pixel_snap = !pixel_snap;
        camera_set_pixel_snap(pixel_snap);
    }

    if (room_mode) {
        var rx = floor(px / room_w);
        var ry = floor(py / room_h);
        var room_x = rx * room_w;
        var room_y = ry * room_h;
        var room_cx = room_x + room_w * 0.5;
        var room_cy = room_y + room_h * 0.5;
        camera_follow_target(room_cx, room_cy);

        var th = 3;
        draw_rect(room_x, room_y, room_w, th, 0.9, 0.9, 0.9);
        draw_rect(room_x, room_y + room_h - th, room_w, th, 0.9, 0.9, 0.9);
        draw_rect(room_x, room_y, th, room_h, 0.9, 0.9, 0.9);
        draw_rect(room_x + room_w - th, room_y, th, room_h, 0.9, 0.9, 0.9);
    } else {
        camera_follow_target(px, py);
    }
    if (Y.Input.down("q")) zoom = zoom + 1.2 * dt;
    if (Y.Input.down("e")) zoom = zoom - 1.2 * dt;
    if (zoom < 0.2) zoom = 0.2;
    if (zoom > 4.0) zoom = 4.0;
    camera_set_zoom(zoom);
    if (Y.Input.down("r")) camera_set_rotation(12);
    else if (Y.Input.down("t")) camera_set_rotation(0);
    if (Y.Input.pressed("space")) camera_shake(18, 0.25, 45);

    draw_grid(200, 2000);
    active.play(false);
    active.draw();
}
